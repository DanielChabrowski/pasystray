# Maintainer: Christoph Gysin <christoph.gysin@gmail.com>
pkgname=pasystray
pkgver=0.2.2
pkgrel=1
pkgdesc="PulseAudio system tray (a replacement for padevchooser)"
arch=('i686' 'x86_64')
url="https://github.com/christophgysin/pasystray"
license=('LGPL')
groups=('multimedia')
depends=('libpulse' 'gtk3' 'libnotify' 'avahi' 'libx11')
makedepends=('automake' 'autoconf')
provides=('pasystray')
conflicts=('pasystray')
replaces=('pasystray')
source=("https://github.com/christophgysin/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('07406cfcd3ec439bbec2d7b41c13fdb9')

build() {
    cd $srcdir/$pkgname-$pkgver

    aclocal
    autoconf
    autoheader
    automake --add-missing
    ./configure --prefix=/usr
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR="$pkgdir/" install
}
